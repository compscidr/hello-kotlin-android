apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: "com.hiya.jacoco-android"
apply plugin: "org.jmailen.kotlinter"
apply plugin: "org.jetbrains.dokka"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"
    defaultConfig {
        applicationId "com.example.myapplication"
        minSdkVersion 15
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        // 1) Make sure to use the AndroidJUnitRunner, of a subclass of it. This requires a dependency on androidx.test:runner, too!
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        // 2) Connect JUnit 5 to the runner
        testInstrumentationRunnerArgument("runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder")
    }

    // 3) Java 8 is required
    compileOptions {
        setSourceCompatibility(JavaVersion.VERSION_1_8)
        setTargetCompatibility(JavaVersion.VERSION_1_8)
    }

    // 4) JUnit 5 will bundle in files with identical paths; exclude them
    packagingOptions {
        exclude("META-INF/LICENSE*")
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
        }
        debug {
            shrinkResources true
            minifyEnabled true
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
            jacoco {
                destinationFile = file("$buildDir/jacoco/moduleTestsCoverage.exec")
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"

    // (Required) Writing and executing Unit Tests on the JUnit Platform
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testImplementation "io.mockk:mockk:1.9.3"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"

    // (Optional) If you need "Parameterized Tests"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"

    // 5) Test Runner, if you don't have it already
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"

    // 6) The instrumentation test companion libraries
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:1.2.0"
    androidTestImplementation "io.mockk:mockk:1.10.3"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:1.2.0"
}
