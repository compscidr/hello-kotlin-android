plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'io.github.reactivecircus.app-versioning' version '1.1.2'
    id 'de.mannodermaus.android-junit5'
    id 'org.jmailen.kotlinter'
    id 'org.jetbrains.dokka'
}

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.example.myapplication"
        minSdkVersion 15
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"
        // 1) Make sure to use the AndroidJUnitRunner, of a subclass of it. This requires a dependency on androidx.test:runner, too!
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        // 2) Connect JUnit 5 to the runner
        testInstrumentationRunnerArgument("runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder")
    }

    // 3) Java 8 is required
    compileOptions {
        setSourceCompatibility(JavaVersion.VERSION_1_8)
        setTargetCompatibility(JavaVersion.VERSION_1_8)
    }

    // 4) JUnit 5 will bundle in files with identical paths; exclude them
    packagingOptions {
        exclude("META-INF/LICENSE*")
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
        }
        debug {
            shrinkResources false
            minifyEnabled false
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation 'androidx.test:core-ktx:1.4.0'
    implementation 'com.squareup.logcat:logcat:0.1'
    implementation "com.google.dagger:hilt-android:${hilt_version}"
    kapt "com.google.dagger:hilt-compiler:${hilt_version}"

    // (Required) Writing and executing Unit Tests on the JUnit Platform
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.0"
    testImplementation "io.mockk:mockk:1.12.7"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.0"

    // (Optional) If you need "Parameterized Tests"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.9.0"

    // 5) Test Runner, if you don't have it already
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:5.9.0"

    // 6) The instrumentation test companion libraries
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:1.3.0"
    androidTestImplementation "io.mockk:mockk:1.12.7"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:1.3.0"
}

jacoco {
    toolVersion = '0.8.7'
}

rootCoverage {
    includeAndroidTestResults true
    generateXml true
}